// Author: @AliSawari 2023

//@version=5
indicator("3C-MA20-MA50", shorttitle = "3C Pattern MA20-MA50", overlay=true)

// MA, RSI, MACD, ATR
ma20 = ta.sma(close, 20)
ma50 = ta.sma(close, 50)
ma200 = ta.sma(close, 200)
rsi = ta.rsi(close, 14)
[macdLine, signalLine, histLine] = ta.macd(close, 12, 26, 9)
atr = ta.atr(14)
// MA for pullback approximation
ma18 = ta.sma(close, 18)
ma48 = ta.sma(close, 48)

// MA comparison & RSI, MACD Validation

isUptrend = (ma20 > ma50) and (ma50 > ma200) and (ma20[1] > ma50[1]) and (ma50[1] > ma200[1]) and (ma20[2] > ma50[2]) and (ma50[2] > ma200[2])
isDowntrend = (ma20 < ma50) and (ma50 < ma200) and (ma20[1] < ma50[1]) and (ma50[1] < ma200[1]) and (ma20[2] < ma50[2]) and (ma50[2] < ma200[2])


RSIValidationUptrend = rsi < 70 
RSIValidationDowntrend = rsi > 30

MACDValidationUptrend = macdLine[1] <= signalLine[1] or macdLine[2] <= signalLine[2]
MACDValidationDowntrend = macdLine[1] >= signalLine[1] or macdLine[2] >= signalLine[2]

// MAs & MACDs increasing in value
isMAsIncreasing = ma20 > ma20[1] and ma50 > ma50[1] and ma20[1] > ma20[2] and ma50[1] > ma50[2]
// MAs & MACDs decreasing in value
isMAsDecreasing = ma20 < ma20[1] and ma50 < ma50[1] and ma20[1] < ma20[2] and ma50[1] < ma50[2]


// is either of the candles are in contact with the MA (approximation -2), in order to detect a Pullback on the MA
isHitting1 = (ma20 <= high and ma20 >= low) or (ma18 <= high and ma18 >= low)
isHitting2 = (ma20[1] <= high[1] and ma20[1] >= low[1]) or (ma18[1] <= high[1] and ma18[1] >= low[1])
isHitting3 = (ma20[2] <= high[2] and ma20[2] >= low[2]) or (ma18[2] <= high[2] and ma18[2] >= low[2])
isHittingMA20 = isHitting1 or isHitting2 or isHitting3
isHitting4 = (ma50 <= high and ma50 >= low) or (ma48 <= high and ma48 >= low)
isHitting5 = (ma50[1] <= high[1] and ma50[1] >= low[1]) or (ma48[1] <= high[1] and ma48[1] >= low[1])
isHitting6 = (ma50[2] <= high[2] and ma50[2] >= low[2]) or (ma48[2] <= high[2] and ma48[2] >= low[2])
isHittingMA50 = isHitting4 or isHitting5 or isHitting6


// if conditions met for Indicator Values
isConditionsMetUptrend = isUptrend and RSIValidationUptrend  and isMAsIncreasing
isConditionsMetDowntrend = isDowntrend and RSIValidationDowntrend and isMAsDecreasing

getShadows(i) =>
    upperShadow = high[i] - math.max(open[i], close[i])
    lowerShadow = math.min(open[i], close[i]) - low[i]
    [upperShadow, lowerShadow]


getBody(i) => math.abs(close[i] - open[i])

[upperShadow1, lowerShadow1] = getShadows(0)
[upperShadow2, lowerShadow2] = getShadows(1)
[upperShadow3, lowerShadow3] = getShadows(2)
body1 = getBody(0)
body2 = getBody(1)
body3 = getBody(2)

// is 3 Candle pattern Buy
is3rdBearish = open[2] > close[2]
isPrevBull = open[1] < close[1]
isCurrentBull = open < close
isHigherClose = close > close[1]
isHigherShadow = high > high[1]
isUpperShadowsSmallerThanBodies = upperShadow1 < body1 and upperShadow2 < body2 and upperShadow3 < body3
isCloseAbove20 = close > ma20
isCloseAbove50 = close > ma50
is3CPatternBuy = is3rdBearish and isPrevBull and isCurrentBull and isHigherClose and isHigherShadow and isUpperShadowsSmallerThanBodies
is3CBuy20 = is3CPatternBuy and isCloseAbove20
is3CBuy50 = is3CPatternBuy and isCloseAbove50

// is 3 Candle pattern Sell
is3rdBullish = open[2] < close[2]
isPrevBear = open[1] > close[1]
isCurrentBear = open > close
isLowerClose = close < close[1]
isLowerShadow = low < low[1]
isLowerShadowsSmallerThanBodies = lowerShadow1 < body1 and lowerShadow2 < body2 and lowerShadow3 < body3
isCloseBelow20 = close < ma20
isCloseBelow50 = close < ma50
is3CPatternSell = is3rdBullish and isPrevBear and isCurrentBear and isLowerClose and isLowerShadow and isLowerShadowsSmallerThanBodies
is3CSell20 = is3CPatternSell and isCloseBelow20
is3CSell50 = is3CPatternSell and isCloseBelow50




// final conditions
conditionUptrend20 = isConditionsMetUptrend and isHittingMA20 and is3CBuy20
conditionUptrend50 = isConditionsMetUptrend and isHittingMA50 and is3CBuy50
conditionDowntrend20 = isConditionsMetDowntrend and isHittingMA20 and is3CSell20
conditionDowntrend50 = isConditionsMetDowntrend and isHittingMA50 and is3CSell50

conditionUptrend = conditionUptrend20 or conditionUptrend50
conditionDowntrend = conditionDowntrend20 or conditionDowntrend50

// styling
labelPositionBuy = low - (ta.atr(30) * 0.6)
LabelPositionSell = high + (ta.atr(30) * 0.6)
labelColorSell = color.red
labelColorBuy = color.blue
labelTextColor = color.white
bgcolor(conditionUptrend ? color.new(color.blue, 90) : na, 0)
bgcolor(conditionUptrend ? color.new(color.blue, 90) : na, -1)
bgcolor(conditionDowntrend ? color.new(color.red, 90) : na, 0)
bgcolor(conditionDowntrend ? color.new(color.red, 90) : na, -1)
plot(ma20, color = color.blue)
plot(ma50, color = color.orange)
plot(ma200, color = color.black)

getCorrectMSG(type) =>
    msg = ""
    if type == "text"
        msg := if conditionUptrend20
            "B3C-20"
        else if conditionUptrend50
            "B3C-50"
        else if conditionDowntrend20
            "S3C-20"
        else if conditionDowntrend50
            "S3C-50"
    if type == "tooltip"
        msg := if conditionUptrend20
            "Buy 3C MA20"
        else if conditionUptrend50
            "Buy 3C MA50"
        else if conditionDowntrend20
            "Sell 3C MA20"
        else if conditionDowntrend50
            "Sell 3C MA50"
    
    msg


// main
if conditionUptrend
    label.new(bar_index, labelPositionBuy, text = getCorrectMSG("text"), style=label.style_label_up, color = labelColorBuy, textcolor=labelTextColor, tooltip = getCorrectMSG("tooltip"))
else if conditionDowntrend
    label.new(bar_index, LabelPositionSell, text = getCorrectMSG("text"), style=label.style_label_down, color = labelColorSell, textcolor=labelTextColor, tooltip = getCorrectMSG("tooltip") )


