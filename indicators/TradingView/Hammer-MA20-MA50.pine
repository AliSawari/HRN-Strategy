// Author: @AliSawari 2023

//@version=5
indicator("Hammer MA20-MA50", shorttitle = "Hammer MA20-MA50", overlay=true)

// MA 
ma20 = ta.sma(close, 20)
ma50 = ta.sma(close, 50)
ma200 = ta.sma(close, 200)
rsi = ta.rsi(close, 14)
// MA for the prev candle
ma20_1 = ta.sma(close[1], 20)
ma50_1 = ta.sma(close[1], 50)
ma200_1 = ta.sma(close[1], 200)
// MA for pullback approximation
ma18 = ta.sma(close, 18)
ma18_1 = ta.sma(close[1], 18)
ma48 = ta.sma(close, 48)
ma48_1 = ta.sma(close[1], 48)


// MA comparison & RSI Validation
isUptrend = (ma20 > ma50) and (ma50 > ma200) and (ma20_1 > ma50_1) and (ma50_1 > ma200_1)
isDowntrend = (ma20 < ma50) and (ma50 < ma200) and (ma20_1 < ma50_1) and (ma50_1 < ma200_1)

RSIValidationUptrend = rsi < 70 
RSIValidationDowntrend = rsi > 30
// MAs increasing in value
isMAsIncreasing = ma20 > ma20_1 and ma50 > ma50_1
// MAs decreasing in value
isMAsDecreasing = ma20 < ma20_1 and ma50 < ma50_1


// is either of the candles are in contact with the MA (approximation -2), in order to detect a Pullback on the MA
isHitting1 = (ma20 <= high and ma20 >= low) or (ma18 <= high and ma18 >= low)
isHitting2 = (ma20_1 <= high[1] and ma20_1 >= low[1]) or (ma18_1 <= high[1] and ma18_1 >= low[1])
isHitting20 = isHitting1 or isHitting2

isHitting3 = (ma50 <= high and ma50 >= low) or (ma48 <= high and ma48 >= low)
isHitting4 = (ma50_1 <= high[1] and ma50_1 >= low[1]) or (ma48_1 <= high[1] and ma48_1 >= low[1])
isHitting50 = isHitting3 or isHitting4

// if conditions met for Indicator Values
isConditionsMetUptrend = isUptrend and RSIValidationUptrend  and isMAsIncreasing
isConditionsMetDowntrend = isDowntrend and RSIValidationDowntrend and isMAsDecreasing


// Hammer Buy Pattern
upperShadowB = high - math.max(open, close)
bodyLengthB = math.abs(close - open)
lowerShadowB = math.min(open, close) - low
prevUpperShadow = high[1] - math.max(open[1], close[1])
prevBodyLengthB = math.abs(close[1] - open[1])
isBodyBiggerThanUpperShadow = bodyLengthB >= upperShadowB
isLowerShadowTwiceTheBody = lowerShadowB >= 2 * bodyLengthB
isCloseLowerThan = math.max(open, close) < math.max(open[1], close[1])
isShadowTrailingBelow = low < low[1]
isPrevNotHammerB = prevBodyLengthB * 2 >= prevUpperShadow
isSixtyPercentBelowMA20 = (low + (0.6 * lowerShadowB)) <= ma20
isSixtyPercentBelowMA50 = (low + (0.6 * lowerShadowB)) <= ma50
isCloseAboveMA20 = math.max(open, close) >= ma20
isCloseAboveMA50 = math.max(open, close) >= ma50
isHammerBuyPattern = isBodyBiggerThanUpperShadow and isLowerShadowTwiceTheBody and isShadowTrailingBelow and isPrevNotHammerB
isHammerBuy20 = isHammerBuyPattern and isCloseAboveMA20 and isSixtyPercentBelowMA20
isHammerBuy50 = isHammerBuyPattern and isCloseAboveMA50 and isSixtyPercentBelowMA50



// Hammer Sell Pattern
upperShadowS = high - math.max(open, close)
bodyLengthS = math.abs(close - open)
lowerShadowS = math.min(open, close) - low
prevLowerShadow = math.min(open[1], close[1]) - low[1]
prevBodyLengthS = math.abs(close[1] - open[1])
isBodyBiggerThanLowerShadow = bodyLengthS >= lowerShadowS
isUpperShadowTwiceTheBody = upperShadowS >= 2 * bodyLengthS
isCloseAboveThan = math.min(open, close) > math.min(open[1], close[1])
isShadowTrailingAbove = high > high[1]
isPrevNotHammerS = prevBodyLengthS * 2 >= prevLowerShadow
isCloseBelowMA20 = math.min(open, close) <= ma20
isCloseBelowMA50 = math.min(open, close) <= ma50
isSixtyPercentAboveMA20 = (high - (0.6 * upperShadowS)) >= ma20
isSixtyPercentAboveMA50 = (high - (0.6 * upperShadowS)) >= ma50
isHammerSellPattern = isBodyBiggerThanLowerShadow and isUpperShadowTwiceTheBody and isShadowTrailingAbove and isPrevNotHammerS 
isHammerSell20 = isHammerSellPattern and isCloseBelowMA20 and isSixtyPercentAboveMA20
isHammerSell50 = isHammerSellPattern and isCloseBelowMA50 and isSixtyPercentAboveMA50


// final conditions
conditionUptrend20 = isConditionsMetUptrend and isHitting20 and isHammerBuy20
conditionUptrend50 = isConditionsMetUptrend and isHitting50 and isHammerBuy50
conditionDowntrend20 = isConditionsMetDowntrend and isHitting20 and isHammerSell20
conditionDowntrend50 = isConditionsMetDowntrend and isHitting50 and isHammerSell50

conditionUptrend = conditionUptrend20 or conditionUptrend50
conditionDowntrend = conditionDowntrend20 or conditionDowntrend50

// styling
labelPositionBuy = low - (ta.atr(30) * 0.6)
LabelPositionSell = high + (ta.atr(30) * 0.6)
labelColorSell = input(color.red, "Label Color Bearish")
labelColorBuy = input(color.blue, "Label Color Bullish")
bgcolor(conditionUptrend ? color.new(color.blue, 90) : na, 0)
bgcolor(conditionDowntrend ? color.new(color.red, 90) : na, 0)
plot(ma20, color = color.blue)
plot(ma50, color = color.orange)
plot(ma200, color = color.black)


getCorrectMSG(type) =>
    msg = ""
    if type == "text"
        msg := if conditionUptrend20
            "H20B"
        else if conditionUptrend50
            "H50B"
        else if conditionDowntrend20
            "H20S"
        else if conditionDowntrend50
            "H50S"
    if type == "tooltip"
        msg := if conditionUptrend20
            "Buy Hammer MA20"
        else if conditionUptrend50
            "Buy Hammer MA50"
        else if conditionDowntrend20
            "Sell Hammer MA20"
        else if conditionDowntrend50
            "Sell Hammer MA50"
    
    msg


// main
if conditionUptrend
    label.new(bar_index, labelPositionBuy, text = getCorrectMSG("text"), style=label.style_label_up, color = labelColorBuy, textcolor=color.white, tooltip = getCorrectMSG("tooltip"))
else if conditionDowntrend
    label.new(bar_index, LabelPositionSell, text = getCorrectMSG("text"), style=label.style_label_down, color = labelColorSell, textcolor=color.white, tooltip = getCorrectMSG("tooltip") )


