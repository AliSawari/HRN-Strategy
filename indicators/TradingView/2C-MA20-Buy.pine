// Author: @AliSawari 2023

//@version=5
indicator("2C-MA20", shorttitle = "2C Pattern MA20", overlay=true)

// ma 
ma20 = ta.sma(close, 20)
ma50 = ta.sma(close, 50)
ma200 = ta.sma(close, 200)
rsi = ta.rsi(close, 14)
// ma for the prev candle
ma20_1 = ta.sma(close[1], 20)
ma50_1 = ta.sma(close[1], 50)
ma200_1 = ta.sma(close[1], 200)
// ma for pullback approximation
ma18 = ta.sma(close, 18)
ma18_1 = ta.sma(close[1], 18)

// MA comparison & RSI Validation
isUpTrend = (ma20 > ma50) and (ma50 > ma200) and (ma20_1 > ma50_1) and (ma50_1 > ma200_1) and rsi < 70
isDownTrend = (ma20 < ma50) and (ma50 < ma200) and (ma20_1 < ma50_1) and (ma50_1 < ma200_1) and rsi > 30

// MAs increasing in value
isMAsIncreasing = ma20 > ma20_1 and ma50 > ma50_1

// is either of the candles are in contact with the MA (approximation -2), in order to detect a Pullback on the MA
isHitting1 = (ma20 <= high and ma20 >= low) or (ma18 <= high and ma18 >= low)
isHitting2 = (ma20_1 <= high[1] and ma20_1 >= low[1]) or (ma18_1 <= high[1] and ma18_1 >= low[1])
isHitting = isHitting1 or isHitting2

// is 2 Candle pattern Buy
isPrevBear = open[1] > close[1]
isCurrentBull = open < close
isHigherClose = close > math.max(open[1], close[1])
isHigherShadow = high > high[1]
body2 = math.abs(open[1] - close[1])
body1 = math.abs(close - open)
shadowLen2 = high[1] - math.max(open[1], close[1])
shadowLen1 = high - math.max(open, close)
isUpperShadowsSmallerThanBodies = shadowLen2 < body2 and shadowLen1 < body1
isCloseAbove20 = close > ma20
is2CBuy = isPrevBear and isCurrentBull and isHigherClose and isUpperShadowsSmallerThanBodies and isCloseAbove20

// final conditions
conditionUptrend = isUpTrend and isMAsIncreasing and isHitting and is2CBuy
condditionDowntrend = isDownTrend

// styling
labelPositionBuy = low - (ta.atr(30) * 0.6)
LabelPositionSell = high + (ta.atr(30) * 0.6)
labelColorSell = input(color.red, "Label Color Bearish")
labelColorBuy = input(color.blue, "Label Color Bullish")


// main
if conditionUptrend
    label.new(bar_index, labelPositionBuy, text="2C-20 B", style=label.style_label_up, color = labelColorBuy, textcolor=color.white, tooltip = "2C-MA20 Buy")

// alerts 
alertcondition(conditionUptrend, title = "New pattern detected", message = "New 2C Pattern on MA20 Detected ")
